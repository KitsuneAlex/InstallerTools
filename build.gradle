plugins {
    id 'java-library'
    id 'maven-publish'
    id 'eclipse'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'com.github.ben-manes.versions' version '0.51.0'
    id 'net.minecraftforge.licenser' version '1.0.1'
    id 'net.minecraftforge.gradleutils' version '[2.3,2.4)'
    id 'net.minecraftforge.gradlejarsigner' version '1.0.5'
}

group = 'net.minecraftforge'
version = gradleutils.tagOffsetVersion
println("Version: $version")
jarSigner.autoDetect('forge')

repositories {
    mavenCentral()
    maven gradleutils.forgeMaven
}

license {
  header file('LICENSE-header.txt')
  newLine false
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(8)
    withSourcesJar()
}

tasks.named('jar').configure {
    manifest {
        attributes([
            'Main-Class': 'net.minecraftforge.installertools.ConsoleTool',
            'Specification-Title':   'Installer Tools',
            'Specification-Vendor':  'Forge Development LLC',
            'Specification-Version':  gradleutils.gitInfo.tag,
            'Implementation-Title':  "$project.group:$project.name",
            'Implementation-Vendor': 'Forge Development LLC',
            'Implementation-Version': project.version
        ] as LinkedHashMap)
    }
    jarSigner.sign(it)
}

tasks.named('shadowJar').configure {
    archiveClassifier = 'fatjar'
    jarSigner.sign(it)
}

artifacts {
    archives shadowJar
}

dependencies {
    implementation 'net.sf.jopt-simple:jopt-simple:6.0-alpha-3'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'de.siegmar:fastcsv:2.2.2'
    implementation 'net.minecraftforge:srgutils:0.5.10'
    implementation 'org.ow2.asm:asm-commons:9.7'
}

publishing {
    publications.register('mavenJava', MavenPublication) {
        from components.java
        artifactId = 'installertools'
        pom {
            name = 'Installer Tools'
            description = 'A collection of command line tools that are useful for the Forge installer, that are not worth being their own standalone projects.'
            url = 'https://github.com/MinecraftForge/InstallerTools'

            gradleutils.pom.setGitHubDetails(pom, 'InstallerTools')

            license gradleutils.pom.Licenses.LGPLv2_1

            developers {
                developer gradleutils.pom.Developers.LexManos
            }
        }
    }
    repositories {
        maven gradleutils.publishingForgeMaven
    }
}
